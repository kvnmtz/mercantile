plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    // Create, Registrate, Milk Lib, Dripstone Fluid Lib
    maven {
        url = "https://mvn.devos.one/snapshots/"
    }

    // Porting Lib
    maven {
        url = "https://mvn.devos.one/releases/"
    }

    // Reach Entity Attributes
    maven {
        url = "https://maven.jamieswhiteshirt.com/libs-release"
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    modImplementation "com.simibubi.create:create-fabric:$project.create_fabric_version"
}

fabricApi {
    configureDataGeneration() {
        modId = rootProject.mod_id
        outputDirectory = file("../common/src/generated/resources")
        addToResources = false
    }
}

processResources {
    def props = [
            'mod_id'                  : rootProject.mod_id,
            'version'                 : rootProject.version,
            'architectury_api_version': rootProject.architectury_api_version,
            'minecraft_version'       : rootProject.minecraft_version,
            'create_version_short'    : rootProject.create_version_short
    ]

    inputs.properties props

    filesMatching('fabric.mod.json') {
        expand props
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'

    exclude '**/mercantile/datagen/**'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
