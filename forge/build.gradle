plugins {
    id 'com.gradleup.shadow'
}

loom {
    forge {
        mixinConfig "mercantile.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    modImplementation "dev.architectury:architectury-forge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')

    modImplementation("com.simibubi.create:create-$rootProject.minecraft_version:$rootProject.create_version:slim") { transitive = false }
    modImplementation "net.createmod.ponder:Ponder-Forge-$rootProject.minecraft_version:$rootProject.ponder_version"
    modImplementation "com.tterrag.registrate:Registrate:$rootProject.registrate_version"
    modCompileOnly "dev.engine-room.flywheel:flywheel-forge-api-$rootProject.minecraft_version:$rootProject.flywheel_version"
    modRuntimeOnly "dev.engine-room.flywheel:flywheel-forge-$rootProject.minecraft_version:$rootProject.flywheel_version"
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.5.0"))
    implementation "io.github.llamalad7:mixinextras-forge:0.5.0"
}

processResources {
    def props = [
            'mod_id'                  : rootProject.mod_id,
            'version'                 : rootProject.version,
            'architectury_api_version': rootProject.architectury_api_version,
            'minecraft_version'       : rootProject.minecraft_version,
            'create_version_short'    : rootProject.create_version_short
    ]

    inputs.properties props

    filesMatching('META-INF/mods.toml') {
        expand props
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
